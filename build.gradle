plugins {
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'jacoco'
}
rootProject.version = generateVersion();
group = 'br.com.eztest'
version = rootProject.version;

repositories {
    mavenCentral()
}


dependencies {
    implementation(platform("org.apache.groovy:groovy-bom:4.0.20"))
    implementation("org.apache.groovy:groovy")
    testImplementation(platform("org.spockframework:spock-bom:2.3-groovy-4.0"))
    testImplementation("org.spockframework:spock-core")
}

test {
    useJUnitPlatform()
}
java {
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(Test) {
    finalizedBy 'jacocoTestReport' // O relatório é sempre gerado após a execução dos testes
    jacoco {
        destinationFile = layout.buildDirectory.file("jacoco/jacocoTest.exec").get().asFile
        classDumpDir = layout.buildDirectory.dir("jacoco/classpathdumps").get().asFile
    }
}

tasks.named('jacocoTestReport') {
    dependsOn 'test' // Os testes são necessários antes de gerar o relatório
    reports {
        xml.required = true
        csv.required = true
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }
}

jacoco {
    toolVersion = '0.8.11'
    reportsDirectory = layout.buildDirectory.dir("customJacocoReportDir")
}
def generateVersion() {
    def props = new Properties()
    def verFile = new File("${rootProject.projectDir}/version.properties")
    def version = ""
    if (verFile.exists()) {
        verFile.withInputStream { props.load(it) }
        def classifier = props['version.classifier']
        version = props['version.major'] + '.' + props['version.minor']
        def buildNumber = props['build.number']
        def suffix = ''
        if ('SNAPSHOT'.equals(classifier)) {
            suffix = '-' + (new Date().format('yyyyMMdd_HHmm'))
        } else if (buildNumber != null) {
            suffix = '.' + buildNumber;
        } else if (System.env['GO_PIPELINE_COUNTER']) {
            suffix = '.' + System.env['GO_PIPELINE_COUNTER']
        } else if (System.env['BITBUCKET_BUILD_NUMBER']) {
            suffix = '.' + System.env['BITBUCKET_BUILD_NUMBER']
        } else if (System.env['ARTIFACT_BUILD_NUMBER']) {
            suffix = '.' + System.env['ARTIFACT_BUILD_NUMBER']
        } else if (System.env['BUILD_NUMBER']) {
            suffix = '.' + System.env['BUILD_NUMBER']
        }
        version += suffix
    } else {
        println "[WARN] Version unknown. Create version.properties with version.major, version.minor, version.classifier properties."
    }
    version
}

processResources.doLast {
    writeFile(new File("${buildDir}/resources/main/VERSION"), rootProject.version)
    writeFile(new File("${projectDir}/VERSION"), rootProject.version)
}

def writeFile(file, content) {
    file.getParentFile().mkdirs()
    file.createNewFile()
    file.withOutputStream { it << content }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = "br.com.eztest"
            artifactId = 'eztest-data'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Eztest Data'
                description = 'Eztest Data'
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}